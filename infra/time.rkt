#lang racket

(require racket/math
         math/base
         math/flonum
         math/bigfloat
         racket/random
         profile)
(require json)

(require rival3
         "profile.rkt"
         "run-sollya.rkt")

(define *sampling-timeout* (make-parameter 50.0)) ; this parameter is used for plots generation

; These parameters are used for latex data
(define *num-tuned-benchmarks* (make-parameter 0))
(define *rival-timeout* (make-parameter 0))
(define *baseline-timeout* (make-parameter 0))
(define *ziv-timeout* (make-parameter 0))
(define *sollya-timeout* (make-parameter 0))

(define (read-from-string s)
  (read (open-input-string s)))


(define (depth-of-expr expr)
  (match expr
    [(list 'TRUE) 0]
    [(list args ...) (+ 1 (apply + (map depth-of-expr args)))]
    [_ 0]))


(define (time-expr rec timeline)
  (define exprs (map read-from-string (hash-ref rec 'exprs)))
  (define vars (map read-from-string (hash-ref rec 'vars)))
  (unless (andmap symbol? vars)
    (raise 'time "Invalid variable list ~a" vars))
  (match-define `(bool flonum ...) (map read-from-string (hash-ref rec 'discs)))
  (define discs (cons boolean-discretization (map (const flonum-discretization) (cdr exprs))))
  
  (define number-of-ops (apply + (map depth-of-expr exprs)))
  (define minimal-precision 53)

  ; Rival machine
  (define start-compile (current-inexact-milliseconds))
  (define rival-machine
    (parameterize ([*rival-max-precision* 32256])
      (rival-compile exprs vars discs)))
  (define compile-time (- (current-inexact-milliseconds) start-compile))

  ; Baseline, Ziv, and Sollya machines
  (define baseline-machine
    (parameterize ([*rival-max-precision* 32256])
      (baseline-compile exprs vars discs)))

  (define ziv-machine
    (parameterize ([*rival-max-precision* 32256])
      (ziv-compile exprs vars discs)))

  (define sollya-machine
    (match (or (equal? (cdr exprs) `((* (fmod (exp x) (sqrt (cos x))) (exp (neg x))))) ; id 65
               (equal? (cdr exprs) `((* (exp (neg w)) (pow l (exp w)))))) ; id 68
      [#t
       (printf "Sollya didn't compile due to the bugs in evaluation of:\n\t~a\n" exprs)
       #f]
      [#f
       (with-handlers ([exn:fail? (λ (e)
                                    (printf "Sollya didn't compile")
                                    (printf "~a\n" e)
                                    #f)])
         (sollya-compile exprs vars minimal-precision))])) ; prec=53 is an imitation of flonum

  (define tuned-bench #f)
  (define times
    (for/list ([pt* (in-list (hash-ref rec 'points))])
      (define pt (first pt*))
      (define sollya-exs #f)
      (define sollya-status 'invalid)
      (define sollya-apply-time 0.0)

      ; --------------------------- Baseline execution ----------------------------------------------
      (define baseline-start-apply (current-inexact-milliseconds))
      (match-define (list baseline-status baseline-exs)
        (parameterize ([*rival-max-precision* 32256])
          (with-handlers ([exn:rival:invalid? (λ (e) (list 'invalid #f))]
                          [exn:rival:unsamplable? (λ (e) (list 'unsamplable #f))])
            (define exs (vector-ref (baseline-apply baseline-machine (list->vector (map bf pt))) 1))
            (list 'valid exs))))
      (define baseline-apply-time (- (current-inexact-milliseconds) baseline-start-apply))
      (define baseline-executions (rival-profile baseline-machine 'executions))
      (define baseline-iteration (rival-profile baseline-machine 'iterations))
      (define baseline-precision
        (if (zero? (vector-length baseline-executions))
            0
            (apply max (vector->list (vector-map execution-precision baseline-executions)))))

      ; --------------------------- Ziv execution ---------------------------------------------------
      (define ziv-start-apply (current-inexact-milliseconds))
      (match-define (list ziv-status ziv-exs)
        (parameterize ([*rival-max-precision* 32256])
          (with-handlers ([exn:rival:invalid? (λ (e) (list 'invalid #f))]
                          [exn:rival:unsamplable? (λ (e) (list 'unsamplable #f))])
            (define exs (vector-ref (ziv-apply ziv-machine (list->vector (map bf pt))) 1))
            (list 'valid exs))))
      (define ziv-apply-time (- (current-inexact-milliseconds) ziv-start-apply))
      (define ziv-executions (rival-profile ziv-machine 'executions))
      (define ziv-iteration (rival-profile ziv-machine 'iterations))
      
      ; --------------------------- Rival execution -------------------------------------------------
      (define rival-start-apply (current-inexact-milliseconds))
      (match-define (list rival-status rival-exs)
        (parameterize ([*rival-max-precision* 32256])
          (with-handlers ([exn:rival:invalid? (λ (e) (list 'invalid #f))]
                          [exn:rival:unsamplable? (λ (e) (list 'unsamplable #f))])
            (define exs (vector-ref (rival-apply rival-machine (list->vector (map bf pt))) 1))
            (list 'valid exs))))
      (define rival-apply-time (- (current-inexact-milliseconds) rival-start-apply))
      (define rival-iter (rival-profile rival-machine 'iterations))
      (define rival-executions (rival-profile rival-machine 'executions))
      
      ; --------------------------- Sollya execution ------------------------------------------------
      (when (and sollya-machine (not (equal? rival-status 'invalid)))
        (set! sollya-apply-time 0.0)
        (with-handlers ([exn:fail? (λ (e)
                                     (printf "Sollya failed")
                                     (printf "~a\n" e)
                                     (sollya-kill sollya-machine)
                                     (set! sollya-machine #f))])
          (match-define (list internal-time external-time exs status)
            (sollya-apply sollya-machine pt #:timeout (*sampling-timeout*)))
          (set! sollya-apply-time external-time)
          (set! sollya-status status)
          (set! sollya-exs exs))
        
        ; -------------------------------- Combining results ----------------------------------------
        (when (and (> baseline-iteration 0) (not tuned-bench))
          (set! tuned-bench #t)
          (*num-tuned-benchmarks* (add1 (*num-tuned-benchmarks*))))

        (define (executions->max-precisions executions)
          (define max-precisions (make-hash))
          (for ([exec (in-vector executions)]
                #:when (>= (execution-number exec) 0))
            (define i (execution-number exec))
            (define precision (execution-precision exec))
            (hash-set! max-precisions i (max precision (hash-ref max-precisions i 0))))
          max-precisions)
        
        ; Store histograms data
        (when (> baseline-iteration 0)
          ;; Rival
          (for ([execution (in-vector rival-executions)])
            (define name (execution-name execution))
            (define precision (execution-precision execution))
            (when (and (equal? rival-status 'valid) (equal? baseline-status 'valid))
              (timeline-push! timeline
                              'mixsample-rival-valid
                              (list (execution-time execution) name precision)))
            (timeline-push! timeline
                            'mixsample-rival-all
                            (list (execution-time execution) name precision)))
          
          ;; Baseline
          (for ([execution (in-vector baseline-executions)])
            (define name (execution-name execution))
            (define precision (execution-precision execution))
            (when (and (equal? rival-status 'valid) (equal? baseline-status 'valid))
              (timeline-push! timeline
                              'mixsample-baseline-valid
                              (list (execution-time execution) name precision)))
            (timeline-push! timeline
                            'mixsample-baseline-all
                            (list (execution-time execution) name precision)))

          ;; Ziv
          (for ([execution (in-vector ziv-executions)])
            (define name (execution-name execution))
            (define precision (execution-precision execution))
            (when (and (equal? rival-status 'valid) (equal? baseline-status 'valid) (equal? ziv-status 'valid))
              (timeline-push! timeline
                              'mixsample-ziv-valid
                              (list (execution-time execution) name precision)))
            (timeline-push! timeline
                            'mixsample-ziv-all
                            (list (execution-time execution) name precision))))

        (define (push-normalized-density! tool precisions max-prec)
          (for ([precision (in-list precisions)])
            (timeline-push! timeline 'density (list tool (~a (exact->inexact (/ precision max-prec)) #:width 5)))))

        (define optimal-precision-list
          (and (equal? rival-status 'valid)
               (equal? baseline-status 'valid)
               (> baseline-iteration 0)
            (vector->list
             (parameterize ([*rival-max-precision* 32256])
               (rival-machine-find-optimal-precisions rival-machine (list->vector (map bf pt)))))))
        
        ; Density plot data
        (when (and optimal-precision-list
                   (equal? ziv-status 'valid))
          (define rival-max-precisions (executions->max-precisions rival-executions))
          (define baseline-max-precisions (executions->max-precisions baseline-executions))
          (define ziv-max-precisions (executions->max-precisions ziv-executions))
          (define optimal-precision-list*
            (for/list ([precision (in-list optimal-precision-list)])
              (max minimal-precision precision)))

          ; In case of constant folding - just assume that the precision was optimal (that way it less contributes to the plot)
          (define rival-precisions-vector (vector-copy (list->vector optimal-precision-list*)))
          (for ([(i precision)
                 (in-hash rival-max-precisions)]) (vector-set! rival-precisions-vector i (max minimal-precision precision)))
          
          (define baseline-precisions-vector (vector-copy (list->vector optimal-precision-list*)))
          (for ([(i precision) (in-hash baseline-max-precisions)])
            (vector-set! baseline-precisions-vector i (max minimal-precision precision)))

          (define ziv-precisions-vector (vector-copy (list->vector optimal-precision-list*)))
          (for ([(i precision) (in-hash ziv-max-precisions)])
            (vector-set! ziv-precisions-vector i (max minimal-precision precision)))
          
          (define rival-precision-list (vector->list rival-precisions-vector))
          (define baseline-precision-list (vector->list baseline-precisions-vector))
          (define ziv-precision-list (vector->list ziv-precisions-vector))
          (define max-prec (apply max (append rival-precision-list baseline-precision-list ziv-precision-list optimal-precision-list*)))
          (push-normalized-density! 'rival rival-precision-list max-prec)
          (push-normalized-density! 'baseline baseline-precision-list max-prec)
          (push-normalized-density! 'ziv ziv-precision-list max-prec)
          (push-normalized-density! 'optimal optimal-precision-list* max-prec))

        ; Close-to-optimal graph
        (when optimal-precision-list
          (define optimal-precisions (list->vector optimal-precision-list))
          (define rival-max-precisions (executions->max-precisions rival-executions))
          (define baseline-max-precisions (executions->max-precisions baseline-executions))
          (define ziv-max-precisions (executions->max-precisions ziv-executions))

          ; In case of constant folding - just assume that the precision was optimal (that way it less contributes to the plot)
          (for ([optimal-precision (in-vector optimal-precisions)]
                [i (in-naturals)])
            (define rival-precision (max minimal-precision (hash-ref rival-max-precisions i optimal-precision)))
            (define baseline-precision (max minimal-precision (hash-ref baseline-max-precisions i optimal-precision)))
            (define ziv-precision (max minimal-precision (hash-ref ziv-max-precisions i optimal-precision)))
            (timeline-push! timeline
                            'optimality
                            (list baseline-iteration
                                  (max minimal-precision optimal-precision)
                                  rival-precision
                                  ziv-precision
                                  baseline-precision))))
        
        ; Percentage of instructions has been executed graph
        (when (and (equal? rival-status 'valid) (equal? baseline-status 'valid))
          ;; Rival
          (for ([execution (in-vector rival-executions)])
            (timeline-push! timeline
                            'instr-executed-cnt
                            (list 'rival (execution-iteration execution) 1)))
          (define rival-ivec-len (rival-profile rival-machine 'instructions))
          (for ([n (in-range (add1 rival-iter))])
            (timeline-push! timeline 'instr-executed-cnt (list 'rival-no-repeats n rival-ivec-len)))
          
          ;; Baseline
          (for ([execution (in-vector baseline-executions)])
            (timeline-push! timeline
                            'instr-executed-cnt
                            (list 'baseline (execution-iteration execution) 1)))
          (define baseline-ivec-len (rival-profile baseline-machine 'instructions))
          (for ([n (in-range (add1 baseline-iteration))])
            (timeline-push! timeline 'instr-executed-cnt (list 'baseline-no-repeats n baseline-ivec-len)))

          ;; Ziv
          (for ([execution (in-vector ziv-executions)])
            (timeline-push! timeline
                            'instr-executed-cnt
                            (list 'ziv (execution-iteration execution) 1)))
          (define ziv-ivec-len (rival-profile ziv-machine 'instructions))
          (for ([n (in-range (add1 ziv-iteration))])
            (timeline-push! timeline 'instr-executed-cnt (list 'ziv-no-repeats n ziv-ivec-len))))

        ; Speed graph and number of points graph
        (when (> (*sampling-timeout*) sollya-apply-time)
          (point-bucketing timeline
                           rival-status
                           rival-apply-time
                           rival-exs
                           baseline-status
                           baseline-apply-time
                           baseline-exs
                           ziv-status
                           ziv-apply-time
                           ziv-exs
                           sollya-status
                           sollya-apply-time
                           sollya-exs
                           baseline-iteration
                           ziv-iteration
                           rival-iter
                           number-of-ops))

        ; Timeouts measuring
        (when (<= (*sampling-timeout*) sollya-apply-time)
          (*sollya-timeout* (add1 (*sollya-timeout*))))
        (when (<= (*sampling-timeout*) rival-apply-time)
          (*rival-timeout* (add1 (*rival-timeout*))))
        (when (<= (*sampling-timeout*) baseline-apply-time)
          (*baseline-timeout* (add1 (*baseline-timeout*))))
        (when (<= (*sampling-timeout*) ziv-apply-time)
          (*ziv-timeout* (add1 (*ziv-timeout*)))))
      
      ; Count differences
      (define rival-baseline-difference
        (if (or (not (equal? rival-status baseline-status)) (not (equal? rival-status ziv-status))) 1 0))
      (cons rival-status (cons rival-apply-time rival-baseline-difference))))
  
  ; Zombie process
  (when sollya-machine
    (sollya-kill sollya-machine))

  (cons (cons 'compile compile-time) times))


(define (time-exprs data)
  (define times
    (for/hash ([group (in-list (group-by car data))])
      (values (caar group) (map cdr group))))

  (list (/ (car (hash-ref times 'compile)) 1000)
        (length (hash-ref times 'valid '()))
        (/ (apply + (map car (hash-ref times 'valid '()))) 1000)
        (length (hash-ref times 'invalid '()))
        (/ (apply + (map car (hash-ref times 'invalid '()))) 1000)
        (length (hash-ref times 'unsamplable '()))
        (/ (apply + (map car (hash-ref times 'unsamplable '()))) 1000)
        (apply + (map cdr (hash-ref times 'unsamplable '())))))

(define (timeline-push! timeline key args*)
  (match key
    ['outcomes
     (match-define (list status rival-iter baseline-iter ziv-iter number-of-ops time*) args*)
     (define outcomes-hash (hash-ref timeline key))
     (match-define (list time num-points)
       (hash-ref outcomes-hash (list status rival-iter baseline-iter ziv-iter number-of-ops) (λ () (list 0 0))))
     (hash-set! outcomes-hash
                (list status rival-iter baseline-iter ziv-iter number-of-ops)
                (list (+ time time*) (+ num-points 1)))]
    [(or 'mixsample-rival-valid
         'mixsample-rival-all
         'mixsample-baseline-valid
         'mixsample-baseline-all
         'mixsample-ziv-valid
         'mixsample-ziv-all)
     (define mixsample-hash (hash-ref timeline key))
     (match-define (list time* name precision) args*)
     (define time (hash-ref mixsample-hash (list name precision) (λ () 0)))
     (hash-set! mixsample-hash (list name precision) (+ time time*))]
    ['instr-executed-cnt
     (define instr-cnt-hash (hash-ref timeline key))
     (match-define (list tool iter cnt) args*)
     (define cnt* (hash-ref instr-cnt-hash (list tool iter) (λ () 0)))
     (hash-set! instr-cnt-hash (list tool iter) (+ cnt cnt*))]
    ['density
     (define density-hash (hash-ref timeline key))
     (match-define (list tool precision) args*)
     (define cnt (hash-ref density-hash (list tool precision) (λ () 0)))
     (hash-set! density-hash (list tool precision) (add1 cnt))]
    ['optimality
     (define optimality-hash (hash-ref timeline key))
     (match-define (list iter optimal-precision rival-precision ziv-precision baseline-precision) args*)
     (match-define (list optimal-total rival-total baseline-total ziv-total cnt)
       (hash-ref optimality-hash iter (λ () (list 0.0 0.0 0.0 0.0 0))))
     (hash-set! optimality-hash
                iter
                (list (+ optimal-total optimal-precision)
                      (+ rival-total rival-precision)
                      (+ baseline-total baseline-precision)
                      (+ ziv-total ziv-precision)
                      (add1 cnt)))]
    [else (error "Unknown key for timeline!")]))

(define (timeline->jsexpr timeline)
  (define (optimality->jsexpr optimality-hash)
    (for/list ([(key value) (in-hash optimality-hash)])
       (define iter key)
       (match-define (list optimal-total rival-total baseline-total ziv-total cnt) value)
       (list iter
             (~a (exact->inexact (/ optimal-total cnt)) #:width 5)
             (~a (exact->inexact (/ rival-total cnt)) #:width 5)
             (~a (exact->inexact (/ baseline-total cnt)) #:width 5)
             (~a (exact->inexact (/ ziv-total cnt)) #:width 5))))
  
  (hash 'outcomes
        (for/list ([(key value) (in-hash (hash-ref timeline 'outcomes))])
          (list (first value) (second key) (third key) (fourth key) (fifth key) (first key) (second value)))
        'mixsample-rival-valid
        (for/list ([(key value) (in-hash (hash-ref timeline 'mixsample-rival-valid))])
          (list value (car key) (second key)))
        'mixsample-rival-all
        (for/list ([(key value) (in-hash (hash-ref timeline 'mixsample-rival-all))])
          (list value (car key) (second key)))
        'mixsample-baseline-valid
        (for/list ([(key value) (in-hash (hash-ref timeline 'mixsample-baseline-valid))])
          (list value (car key) (second key)))
        'mixsample-baseline-all
        (for/list ([(key value) (in-hash (hash-ref timeline 'mixsample-baseline-all))])
          (list value (car key) (second key)))
        'mixsample-ziv-valid
        (for/list ([(key value) (in-hash (hash-ref timeline 'mixsample-ziv-valid))])
          (list value (car key) (second key)))
        'mixsample-ziv-all
        (for/list ([(key value) (in-hash (hash-ref timeline 'mixsample-ziv-all))])
          (list value (car key) (second key)))
        'instr-executed-cnt
        (for/list ([(key value) (in-hash (hash-ref timeline 'instr-executed-cnt))])
          (list (~a (car key)) (second key) value))
        'density
        (for/list ([(key value) (in-hash (hash-ref timeline 'density))])
          (list (~a (first key)) (second key) value))
        'optimality
        (optimality->jsexpr (hash-ref timeline 'optimality))))

(define (make-expression-table points test-id timeline-port)
  (newline)
  (define total-c 0.0)
  (define total-v 0.0)
  (define count-v 0.0)
  (define total-i 0.0)
  (define count-i 0.0)
  (define total-u 0.0)
  (define count-u 0.0)
  (define total-mem-bytes 0)

  (define timeline
    (make-hash ; this hash is to be used for the plots
     (list (cons 'outcomes (make-hash))
           (cons 'mixsample-rival-valid (make-hash))
           (cons 'mixsample-baseline-valid (make-hash))
           (cons 'mixsample-ziv-valid (make-hash))
           (cons 'mixsample-rival-all (make-hash))
           (cons 'mixsample-baseline-all (make-hash))
           (cons 'mixsample-ziv-all (make-hash))
           (cons 'instr-executed-cnt (make-hash))
           (cons 'density (make-hash))
           (cons 'optimality (make-hash)))))

  (define table
    (for/list ([rec (in-port read-json points)]
               [i (in-naturals)]
               #:break (and test-id (> i (string->number test-id)))
               #:unless (and test-id (not (equal? (~a i) test-id))))
      (when test-id
        (pretty-print (map read-from-string (hash-ref rec 'exprs))))

      (define mem-before (current-memory-use 'cumulative))
      (match-define (list c-time v-num v-time i-num i-time u-num u-time _)
        (time-exprs (time-expr rec timeline)))
      (define mem-after (current-memory-use 'cumulative))
      (define mem-delta (- mem-after mem-before))
      (define mem-mib (/ (exact->inexact mem-delta) (* 1024 1024)))
      (set! total-c (+ total-c c-time))
      (set! total-v (+ total-v v-time))
      (set! count-v (+ count-v v-num))
      (set! total-i (+ total-i i-time))
      (set! count-i (+ count-i i-num))
      (set! total-u (+ total-u u-time))
      (set! count-u (+ count-u u-num))
      (set! total-mem-bytes (+ total-mem-bytes mem-delta))
      (define t-time (+ c-time v-time i-time u-time))
      (printf "~a: ~as ~as ~as ~as ~as MiB\n"
              (~a i #:align 'left #:min-width 3)
              (~r t-time #:precision '(= 3) #:min-width 8)
              (~r v-time #:precision '(= 3) #:min-width 8)
              (~r i-time #:precision '(= 3) #:min-width 8)
              (~r u-time #:precision '(= 3) #:min-width 8)
              (~r mem-mib #:precision '(= 3) #:min-width 8))
      (list i t-time c-time v-num v-time i-num i-time u-num u-time mem-mib)))
  (printf "\nDATA:\n")
  (printf "\tNUMBER OF TUNED BENCHMARKS = ~a\n" (*num-tuned-benchmarks*))
  (printf "\tRIVAL TIMEOUTS = ~a\n" (*rival-timeout*))
  (printf "\tBASELINE TIMEOUTS = ~a\n" (*baseline-timeout*))
  (printf "\tZIV TIMEOUTS = ~a\n" (*ziv-timeout*))
  (printf "\tSOLLYA TIMEOUTS = ~a\n" (*sollya-timeout*))

  (when timeline-port
    (write-json (timeline->jsexpr timeline) timeline-port)
    (close-output-port timeline-port))

  (define total-t (+ total-c total-v total-i total-u))
  (define total-mem (/ (exact->inexact total-mem-bytes) (* 1024 1024)))
  (printf "\nTotal Time: ~as\n" (~r total-t #:precision '(= 3)))
  (printf "Total Memory: ~a MiB\n" (~r total-mem #:precision '(= 3)))
  (define footer
    (list "Total" total-t total-c count-v total-v count-i total-i count-u total-u total-mem))
  (values table footer))

(define (html-write port)
  (define sortable-css "https://cdn.jsdelivr.net/gh/tofsjonas/sortable@latest/sortable.min.css")
  (define sortable-js "https://cdn.jsdelivr.net/gh/tofsjonas/sortable@latest/sortable.min.js")
  (when port
    (fprintf port "<!doctype html><meta charset=utf-8 />")
    (fprintf port "<link href='~a' rel='stylesheet' />" sortable-css)
    (fprintf port "<script src='profile.js' defer></script>")
    (fprintf port "<script src='~a' async defer></script>" sortable-js)
    (fprintf
     port
     "<style>body { max-width: 100ex; margin: 3em auto; } td:nth-child(1n+2) { text-align: right; }</style>")))

(define current-heading #f)

(define (html-write-table port name cols)
  (set! current-heading cols)
  (when port
    (fprintf port "<h1>~a</h1>" name)
    (fprintf port "<table class=sortable>")
    (fprintf port "<thead><tr>")
    (for ([col (in-list cols)])
      (define name
        (match col
          [(list name _) name]
          [name name]))
      (fprintf port "<th>~a</th>" name))
    (fprintf port "</tr></thead><tbody>")))

(define (html-write-row port row)
  (when port
    (fprintf port "<tr>")
    (for ([cell (in-list row)]
          [heading (in-list current-heading)])
      (define unit
        (match heading
          [(list _ s) s]
          [_ ""]))
      (cond
        [(and (number? cell) (zero? cell)) (fprintf port "<td></td>")]
        [(integer? cell) (fprintf port "<td>~a~a</td>" (~r cell #:group-sep " ") unit)]
        [(real? cell)
         (fprintf port "<td data-sort=~a>~a~a</td>" cell (~r cell #:precision '(= 2)) unit)]
        [else (fprintf port "<td><code>~a</code></td>" cell)]))
    (fprintf port "</tr>")))

(define (html-end-table port)
  (when port
    (fprintf port "</table>")))

(define (html-write-footer port row)
  (when port
    (fprintf port "<tfoot>")
    (html-write-row port row)))

(define (html-write-profile port)
  (when port
    (fprintf port "<section id='profile'><h1>Profiling</h1>")
    (fprintf port "<p class='load-text'>Loading profile data...</p></section>")))

(define (run test-id p timeline-port)
  (define-values (expression-table expression-footer)
    (if (and p (or (not test-id) (string->number test-id)))
        (make-expression-table p test-id timeline-port)
        (values #f #f)))
  (list expression-table expression-footer))

(define (html-add-plot port path #:width width #:height height)
  (when port
    (fprintf port (format "<img src=\"~a\" width=\"~a\" height=\"~a\">" path width height))))

(define (generate-html html-port profile-port expression-table expression-footer dir)
  (html-write html-port)

  (when expression-table
    (define cols
      '("#" ("Total" "s")
            ("Compile" "s")
            "Valid"
            ("(s)" "s")
            "Invalid"
            ("(s)" "s")
            "Unable"
            ("(s)" "s")
            ("Memory" "MiB")))
    (html-write-table html-port "Expression timing" cols)
    (for ([row (in-list expression-table)])
      (html-write-row html-port row))
    (when expression-footer
      (html-write-footer html-port expression-footer))
    (html-end-table html-port))

  (when expression-table
    (html-add-plot html-port "ratio_plot_precision.png" #:width 400 #:height 250)
    (html-add-plot html-port "cnt_per_iters_plot.png" #:width 400 #:height 300)
    (html-add-plot html-port "density_cdf_plot.png" #:width 400 #:height 300)
    (html-add-plot html-port "optimality_plot.png" #:width 400 #:height 300)
    (html-add-plot html-port "histogram_valid.png" #:width 650 #:height 275))

  (when profile-port
    (html-write-profile html-port)))

(define (profile-json-renderer profile-port)
  (lambda (p order)
    (when profile-port
      (write-json (profile->json p) profile-port))))

(module+ main
  (require racket/cmdline)
  (define dir #f)
  (define html-port #f)
  (define timeline-port #f)
  (define profile-port #f)
  (define n #f)
  (command-line
   #:once-each
   [("--dir")
    fn
    "Directory to produce html outputs"
    (set! dir fn)
    (when dir
      (set! timeline-port
            (open-output-file (format "~a/timeline.json" dir) #:mode 'text #:exists 'replace))
      (set! html-port
            (open-output-file (format "~a/index.html" dir) #:mode 'text #:exists 'replace)))]
   [("--profile")
    fn
    "Produce a JSON profile"
    (set! profile-port (open-output-file fn #:mode 'text #:exists 'replace))]
   [("--id") ns "Run a single test" (set! n ns)]
   #:args ([points "infra/points.json"])
   (match-define (list ex-t ex-f)
     (let ([points-port (open-input-file points)])
       (if profile-port
           (profile #:order 'total
                    #:delay 0.001
                    #:render (profile-json-renderer profile-port)
                    (run n points-port timeline-port))
           (run n points-port timeline-port))))
   (when dir
     (generate-html html-port profile-port ex-t ex-f dir))))

(define (point-bucketing timeline
                         rival-status
                         rival-time
                         rival-exs
                         baseline-status
                         baseline-time
                         baseline-exs
                         ziv-status
                         ziv-time
                         ziv-exs
                         sollya-status
                         sollya-time
                         sollya-exs
                         baseline-iter
                         ziv-iter
                         rival-iter
                         number-of-ops)

  (define (status-subbucketing status exs)
    (cond
      [(or (equal? exs (fl 0.0)) (equal? exs (fl -0.0))) (format "~a-zero" status)]
      [(flinfinite? exs) (format "~a-inf" status)]
      [else (format "~a-real" status)]))

  (define (push-outcome! status time*)
    (timeline-push! timeline  'outcomes (list status rival-iter baseline-iter ziv-iter number-of-ops time*)))

  (cond
    ; Rival has produced valid outcomes
    [(equal? rival-status 'valid)
     (cond
       ; Every tool have succeded
       ; These points will go into speed graph
       [(and (equal? 'valid sollya-status)
             (equal? 'valid baseline-status)
             (equal? 'valid ziv-status)
             (equal? 'valid rival-status)
             (> (*sampling-timeout*) sollya-time)
             (> (*sampling-timeout*) rival-time)
             (> (*sampling-timeout*) baseline-time)
             (> (*sampling-timeout*) ziv-time))
        (push-outcome! "valid-sollya" sollya-time)
        (push-outcome! "valid-baseline" baseline-time)
        (push-outcome! "valid-ziv" ziv-time)
        (push-outcome! "valid-rival" rival-time)
        (if (or (fl= rival-exs sollya-exs)
                (and (fl= rival-exs (fl 0.0)) (fl= sollya-exs (fl -0.0)))
                (and (fl= rival-exs (fl -0.0)) (fl= sollya-exs (fl 0.0))))
            (push-outcome! "sollya-correct-rounding" 0)
            (push-outcome! "sollya-faithful-rounding" 0))]

       ; Ziv's points do not go into point graph
       ; Baseline and Rival have succeeded
       [(and (equal? 'valid baseline-status) (equal? rival-status 'valid))
        (push-outcome! (status-subbucketing "valid-rival+baseline" rival-exs) rival-time)]

       ; Baseline and Sollya have succeeded
       [(and (equal? 'valid sollya-status) (equal? 'valid baseline-status))
        (push-outcome! (status-subbucketing "valid-sollya+baseline" baseline-exs) sollya-time)]

       ; Sollya and Rival have succeeded
       [(and (equal? 'valid sollya-status) (equal? rival-status 'valid))
        (push-outcome! (status-subbucketing "valid-rival+sollya" rival-exs) rival-time)]

       ; Only Rival has succeeded
       [(equal? rival-status 'valid)
        (push-outcome! (status-subbucketing "valid-rival-only" rival-exs) rival-time)]

       ; Only Sollya has succeeded
       [(equal? 'valid sollya-status)
        (push-outcome! (status-subbucketing "valid-sollya-only" sollya-exs) sollya-time)]

       ; Only Baseline has succeeded
       [(equal? 'valid baseline-status)
        (push-outcome! (status-subbucketing "valid-baseline-only" baseline-exs) baseline-time)]

       ; timeout at all the tools
       [else
        (push-outcome! "exit-baseline" baseline-time)
        (push-outcome! "exit-sollya" sollya-time)
        (push-outcome! "exit-rival" rival-time)])]

    ; Rival has exited
    [(equal? rival-status 'unsamplable)
     (cond
       ; Sollya and Baseline have succeeded
       [(and (equal? 'valid sollya-status) (equal? 'valid baseline-status))
        (push-outcome! (status-subbucketing "valid-sollya+baseline" baseline-exs) sollya-time)]

       ; Only Sollya has succeeded
       [(equal? 'valid sollya-status)
        (push-outcome! (status-subbucketing "valid-sollya-only" sollya-exs) sollya-time)]

       ; Only Baseline has succeeded
       [(equal? 'valid baseline-status)
        (push-outcome! (status-subbucketing "valid-baseline-only" baseline-exs) baseline-time)]

       ; Points that every tools fail to evaluate when the precision is unreacheble
       [else
        (push-outcome! "exit-baseline" baseline-time)
        (push-outcome! "exit-sollya" sollya-time)
        (push-outcome! "exit-rival" rival-time)])]))
